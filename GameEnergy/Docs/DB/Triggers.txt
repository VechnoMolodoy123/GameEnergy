SELECT * FROM sys.triggers;

CREATE TRIGGER trg_CalculateDiscountedPrice
ON [dbo].[Games]
AFTER INSERT, UPDATE
AS
BEGIN
    SET NOCOUNT ON;

    UPDATE g
    SET DiscountedPrice = 
        CASE 
            WHEN i.Discount IS NULL OR i.Discount <= 0 THEN i.Price
            WHEN i.Discount >= 100 THEN 1  -- минимум 1 копейка
            ELSE i.Price * (100 - i.Discount) / 100  -- целочисленное деление
        END
    FROM [dbo].[Games] g
    INNER JOIN inserted i ON g.GameID = i.GameID;
END;

CREATE TRIGGER trg_UpdateAverageRatingAfterInsert
ON [dbo].[Rating]
AFTER INSERT
AS
BEGIN
    SET NOCOUNT ON;
    UPDATE g
    SET AverageRating = (
        SELECT AVG(r.Rating * 1.0)
        FROM [dbo].[Rating] r
        WHERE r.GameID = g.GameID
    )
    FROM [dbo].[Games] g
    INNER JOIN inserted i ON g.GameID = i.GameID;
END;

CREATE TRIGGER trg_UpdateAverageRatingAfterDelete
ON [dbo].[Rating]
AFTER DELETE
AS
BEGIN
    SET NOCOUNT ON;
    UPDATE g
    SET AverageRating = COALESCE(
        (SELECT AVG(r.Rating * 1.0) FROM [dbo].[Rating] r WHERE r.GameID = g.GameID),
        0.00
    )
    FROM [dbo].[Games] g
    INNER JOIN deleted d ON g.GameID = d.GameID;
END;

CREATE TRIGGER trg_UpdateAverageRatingAfterUpdate
ON [dbo].[Rating]
AFTER UPDATE
AS
BEGIN
    SET NOCOUNT ON;
    IF UPDATE(Rating)
    BEGIN
        UPDATE g
        SET AverageRating = (
            SELECT AVG(r.Rating * 1.0)
            FROM [dbo].[Rating] r
            WHERE r.GameID = g.GameID
        )
        FROM [dbo].[Games] g
        INNER JOIN inserted i ON g.GameID = i.GameID;
    END
END;

CREATE TRIGGER [dbo].[trg_UpdateCartTotal]
ON [dbo].[CartItems]
AFTER INSERT, UPDATE, DELETE
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @CartIDs TABLE (CartID INT);
    INSERT INTO @CartIDs
    SELECT CartID FROM inserted
    UNION
    SELECT CartID FROM deleted;

    UPDATE c
    SET 
        TotalAmount = ISNULL((
            SELECT SUM(ci.PriceAtAdd)
            FROM [dbo].[CartItems] ci
            WHERE ci.CartID = c.CartID
        ), 0),
        ModifiedDate = GETDATE()
    FROM [dbo].[Cart] c
    INNER JOIN @CartIDs t ON c.CartID = t.CartID;
END;